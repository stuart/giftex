gift <- (command* question blank_line?)+;

command <- '$' (!line_break .)* line_break;

question <- essay_question / true_false_question / matching_question / fill_in_question / short_answer_question / multiple_choice_question / numeric_question / description;

% QUESTION TYPES
essay_question <- question_text '{' nbsp? '}';

true_false_question <- question_text '{' space (true_answer / false_answer) space '}';

matching_question <- question_text "{" space (match_answer)+ space "}";

fill_in_question <- question_text '{' space (wrong_answer / right_answer)+ space  '}' (![\r\n] (escaped_symbol / .))+;

short_answer_question <- question_text "{" space (right_answer)+ space "}";

multiple_choice_question <- question_text "{" space (right_answer / wrong_answer)+ space "}";

numeric_question <- question_text "{#" space (numeric_answer)+ space "}";

description <- question_text;

question_text <- (!"{" !blank_line ( escaped_symbol / .))+;

% ANSWER TYPES
true_answer <- ("TRUE" / "T") space feedback?;

false_answer <- ("FALSE" / "F") space feedback?;

wrong_answer <- "~" weight? escaped_text feedback?;

right_answer <- "=" weight? escaped_text feedback?;

match_answer <- "=" (!"->" ![=}#] (escaped_symbol / .))* "->" (![=}#] (escaped_symbol / .))* space;

numeric_answer <- "="? weight? (numeric_with_tolerance / range / number) space feedback?;

% OTHER types
range <- number ".." number;

numeric_with_tolerance <- number ":" number;

feedback <- "#" escaped_text;

weight <- "%" number "%";

number <- int frac?;

int <- '-'? (non_zero_digit digit+) / ('-'? digit);

frac <- '.' digit+;

non_zero_digit <- [1-9];

digit <- [0-9];

blank_line <- line_break nbsp? line_break space;

nbsp <- (" " / "\t")+;

line_break <- [\n\r]+;

space <- [ \t\n\s\r]*;

escaped_text <- (![=~}#] ( escaped_symbol / .))+ space;

escaped_symbol <- ("\\" [={}#:]);
